#!/usr/bin/env zsh

function __zgenom_compile() {
    if [ $file -nt $file.zwc ] && [[ -r $file ]]; then
        zcompile $opts $file
    fi
}

function zgenom-compile() {
    local opts=()
    zparseopts -a opts -D -- U k z R M || return
    local file=$1
    if [ -z $file ]; then
        __zgenom_err '`compile` requires one parameter:'
        __zgenom_err '`zgenom compile <location>`'
    elif [ -f $file ]; then
        __zgenom_compile
    else
        for file in $file/**/*(DN); do
            # only files and ignore compiled files
            if [ ! -f $file ] || [[ $file = *.zwc ]]; then
                continue

            # Check *.sh if it can be parsed from zsh
            elif [[ $file = *.sh ]]; then
                if ! zsh -n $file 2>/dev/null; then
                    continue
                fi

            # Check for shebang if not:
            # - zsh startup file
            # - *.zsh
            # - zcompdump*
            elif [[ $file != *.zsh ]] \
                && [[ $file != *zcompdump* ]] \
                && [[ ! $file =~ '\.z(shenv|profile|shrc|login|logout)$' ]]; then
                read -r firstline < $file
                if [[ ! $firstline =~ '^#!.*zsh' ]] 2>/dev/null; then
                    continue
                fi
            fi

            __zgenom_compile
        done
    fi
}

zgenom-compile $@
