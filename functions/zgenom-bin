#!/usr/bin/env zsh

function __zgenom_bin() {
    local file="${1}"
    local name="${2:-${file:t}}"
    local destination="$ZGENOM_SOURCE_BIN/$name"
    if [[ ! -e $destination ]] ln -s $file $destination
}

function zgenom-bin() {
    if [[ "$#" == 0 ]]; then
        __zgenom_err '`bin` requires at least one parameter:'
        __zgenom_err '`zgenom bin <repo> [location] [branch] [name]`'
        return
    fi
    local repo="${1}"
    local location="${2%/}"
    local branch="${3}"
    local name="${4}"
    local dir="$(__zgenom_clone_dir ${repo} --branch ${branch})"

    zgenom-clone "${repo}" "${branch}"

    if [[ ! -d $ZGENOM_SOURCE_BIN ]] mkdir -p $ZGENOM_SOURCE_BIN

    if [[ -n $location ]]; then
        location="${dir}/${location}"
        if [[ -f "${location}" ]]; then
            __zgenom_bin "${location}" $name
            return
        fi
    elif [[ -d "${dir}/bin" ]]; then
        location="${dir}/bin"
    else
        location="${dir}"
    fi

    setopt localoptions nullglob
    for file in ${location}/*; do
        if [[ -x $file ]]; then
            __zgenom_bin "$file"
        fi
    done
}

zgenom-bin $@
